# Execution begins at address 0 
	.pos 0
	irmovq stack, %rsp  	# Set up stack pointer
	call main		# Execute main program
	halt			# Terminate program 

main:	irmovq ele1,%rdi
	call sum_list		# sum_list(list)
	ret

# Sample linked list
        .align 8
  ele1:
        .quad 0x00a
        .quad ele2
  ele2:
        .quad 0x0b0
        .quad ele3
  ele3:
        .quad 0xc00
        .quad 0

sum_list: xorq %rax,%rax
    andq    %rdi  , %rdi
    je      end 
loop: mrmovq (%rdi), %rcx 
    addq    %rcx  , %rax
    irmovq  $8    , %rcx
    addq    %rcx  , %rdi
    mrmovq  (%rdi), %rdi
    andq    %rdi  , %rdi
    jne loop
end:
    ret             

# Stack starts here and grows to lower addresses
	.pos 0x200
stack:
